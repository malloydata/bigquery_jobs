import "jobs_table.malloy"

source: jobs is jobs_table + {
  rename: 
    bytes_billed is total_bytes_billed
    bytes_processed is total_bytes_processed
    slot_ms is total_slot_ms
    sql_query is query
    
  dimension: 
    billed_gb is bytes_billed / (1024*1024*1024)
    is_query is job_type = 'QUERY'
    slot_seconds is (slot_ms/1000)
    elapsed_seconds is (unix_millis!(end_time)::number - unix_millis!(start_time)::number)/1000
    query_hash is to_base64!(md5!(sql_query))::string
    is_service_account is user_email ~ r'gserviceaccount'

  measure:
    job_count is count()
    query_count is job_count {? is_query }
    distinct_query_count is count(distinct sql_query)
    total_billed_gb is billed_gb.sum()
    total_slot_seconds is slot_ms.sum()/1000
    total_slot_minutes is total_slot_seconds/60
    user_count is count(distinct user_email)
    time_range is concat(min(start_time)::date:: string, ' to ',  max(start_time)::date::string)
    # percent
    percent_cached_query is query_count {? cache_hit} / nullif(query_count,0)
    stage_count is timeline.count()
    percent_of_gb_billed is total_billed_gb/all(total_billed_gb)*100

  query: cached is {
    group_by: cache_hit
    aggregate: query_count, total_billed_gb
  }

  query: query_detail is {
    where: is_query
    group_by: 
      job_id
      user_email
      start_time
      billed_gb
      slot_seconds 
      elapsed_seconds
    nest: data_sets_list is {
      group_by: referenced_tables.dataset_id
    }
    aggregate: 
      stage_count
  }

  query: by_email is {
    group_by: user_email
    aggregate: 
      total_billed_gb
      query_count 
  }

  by_dataset_list is {
    group_by: referenced_tables.dataset_id
  }

  query: by_query is {
    group_by: query_hash
    aggregate: 
      total_billed_gb
      percent_of_gb_billed
      query_count  
      total_slot_minutes
      user_count
      percent_cached_query
    nest: 
      # list
      by_email is {group_by: user_email}
      # list
      by_dataset is {
        group_by: referenced_tables.dataset_id 
        where: referenced_tables.dataset_id != null
      }
  }

  // # view='query_analysis'
  query: query_analysis is by_query + {
    group_by: sql_query
    aggregate: 
      max_slot_seconds is max(slot_seconds)
      average_slot_seconds is avg(slot_seconds)
    nest: referenced_tables is {
      group_by: 
        referenced_tables.dataset_id
        referenced_tables.table_id
    }
  }

  query: by_date is {
    group_by: 
      query_date is start_time::date
      is_service_account
    aggregate: 
      total_billed_gb
      query_count
      total_slot_minutes
      user_count
      percent_cached_query
  }

  query: email_date is  by_date + {
    where: user_email != null
    group_by: user_email
  }

  query: dataset_date is by_date +  {
    // add dataset dimensions to the query
    where: referenced_tables.dataset_id != null
    group_by: referenced_tables.dataset_id
  }

  query: table_date is by_date + {
    where: referenced_tables.table_id != null
    group_by: 
      referenced_tables.dataset_id
      referenced_tables.table_id
  }
}
